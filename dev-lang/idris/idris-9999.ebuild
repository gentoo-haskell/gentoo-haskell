# Copyright 1999-2014 Gentoo Foundation
# Distributed under the terms of the GNU General Public License v2
# $Header:  $

# ebuild generated by hackport 0.2.14

EAPI="5"

if [[ ${PV} = 9999* ]]; then
	GIT_ECLASS="git-2"
	DOC_DEPS="doc? ( dev-texlive/texlive-latex )"
fi

CABAL_FEATURES="bin"
inherit haskell-cabal eutils ${GIT_ECLASS}

DESCRIPTION="Dependently Typed Functional Programming Language"
HOMEPAGE="http://www.idris-lang.org/"
if [[ $PV != 9999* ]]; then
	SRC_URI="mirror://hackage/packages/archive/${PN}/${PV}/${P}.tar.gz"
fi
EGIT_REPO_URI="git://github.com/idris-lang/Idris-dev.git"

LICENSE="BSD"
SLOT="0"
KEYWORDS=""
IUSE="doc examples +llvm noeffects"

RDEPEND="dev-haskell/libffi
	dev-libs/gmp"

DEPEND="${RDEPEND}
	dev-haskell/ansi-terminal
	dev-haskell/binary
	>=dev-haskell/cabal-1.16.0
	>=dev-haskell/haskeline-0.7
	>=dev-haskell/language-java-0.2.2
	dev-haskell/mtl
	>=dev-haskell/parsec-3
	>=dev-haskell/parsers-0.9
	dev-haskell/split
	dev-haskell/text
	dev-haskell/transformers
	>=dev-haskell/trifecta-1.1
	dev-haskell/unordered-containers
	dev-haskell/utf8-string
	dev-haskell/vector
	dev-haskell/vector-binary-instances
	>=dev-lang/ghc-7.6.1
	${DOC_DEPS}
	llvm? ( >=dev-haskell/llvm-general-3.3.8 <dev-haskell/llvm-general-3.3.9
		>=dev-haskell/llvm-general-pure-3.3.8 <dev-haskell/llvm-general-pure-3.3.9
		dev-libs/boehm-gc )
"

src_prepare() {
	if [[ ${PV} = 9999* ]]; then
		rm -f "${S}/tutorial/idris-tutorial.pdf"
	fi
}

src_configure() {
	haskell-cabal_src_configure \
		$(cabal_flag llvm llvm) \
		$(cabal_flag noeffects noeffects)
}

src_compile() {
	haskell-cabal_src_compile
	if [[ ${PV} = 9999* ]]; then
		if use doc; then
			pushd tutorial || die "Could not cd to tutorial"
			emake
			popd tutorial
		fi
	fi
}

src_install() {
	haskell-cabal_src_install
	if [[ ${PV} = 9999* ]]; then
		if use doc; then
			dodoc "${S}/tutorial/idris-tutorial.pdf"
		fi
	fi
	if use examples; then
		insinto "/usr/share/${P}/examples"
		doins -r "${S}"/tutorial/examples/*.idr
	fi
}
