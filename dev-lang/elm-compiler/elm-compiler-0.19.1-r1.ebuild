# Copyright 1999-2022 Gentoo Authors
# Distributed under the terms of the GNU General Public License v2

EAPI=8

# ebuild generated by hackport 0.5.6.9999

CABAL_FEATURES="profile haddock hscolour"
inherit haskell-cabal

DESCRIPTION="The 'elm' command line interface"
HOMEPAGE="http://elm-lang.org"
SRC_URI="https://github.com/elm/compiler/archive/${PV}.tar.gz -> ${P}.tar.gz"

LICENSE="BSD"
SLOT="0/${PV}"
KEYWORDS="~amd64 ~x86"
IUSE="dev"

RDEPEND=" !dev-lang/elm-compiler-bin[symlink]
	>=dev-haskell/ansi-terminal-0.8:=
	>=dev-haskell/ansi-wl-pprint-0.6.8:= <dev-haskell/ansi-wl-pprint-0.7:=
	>=dev-haskell/edit-distance-0.2:= <dev-haskell/edit-distance-0.3:=
	dev-haskell/file-embed:=
	dev-haskell/filelock:=
	dev-haskell/haskeline:=
	>=dev-haskell/http-4000.2.5:= <dev-haskell/http-4000.4:=
	>=dev-haskell/http-client-0.6:= <dev-haskell/http-client-0.8:=
	>=dev-haskell/http-client-tls-0.3:= <dev-haskell/http-client-tls-0.4:=
	>=dev-haskell/http-types-0.12:= <dev-haskell/http-types-1.0:=
	>=dev-haskell/language-glsl-0.3:=
	>=dev-haskell/mtl-2.2.1:= <dev-haskell/mtl-3:=
	>=dev-haskell/network-2.4.0.0:=
	dev-haskell/parsec:=
	dev-haskell/raw-strings-qq:=
	dev-haskell/scientific:=
	dev-haskell/sha:=
	dev-haskell/snap-core:=
	dev-haskell/snap-server:=
	dev-haskell/unordered-containers:=
	dev-haskell/utf8-string:=
	dev-haskell/vector:=
	dev-haskell/zip-archive:=
	>=dev-lang/ghc-8.2.1:=
"

DEPEND="${RDEPEND}
	>=dev-haskell/cabal-2.0.0.2
"

PATCHES=("${FILESDIR}"/${PN}-0.19.1-fetch-hack.patch)

S=${WORKDIR}/compiler-${PV}

src_prepare() {
	default

	CABAL_FILE=elm.cabal cabal_chdeps \
		'ansi-terminal >= 0.8 && < 0.9' 'ansi-terminal >= 0.8' \
		'containers >= 0.5.8.2 && < 0.6' 'containers >= 0.5.8.2' \
		'network >= 2.4 && < 2.7' 'network >= 2.4.0.0' \
		'http-client >= 0.6 && < 0.7' 'http-client >= 0.6 && < 0.8'

	# =fail is useful to detect uncached entries
	export ELM_FETCH_MODE_GENTOO=warn
}

src_configure() {
	haskell-cabal_src_configure \
		$(cabal_flag dev dev)
}
