# Copyright 1999-2011 Gentoo Foundation
# Distributed under the terms of the GNU General Public License v2
# $Header:  $

# ebuild generated by hackport 0.2.13

EAPI="3"

CABAL_FEATURES="bin lib profile haddock hscolour"
inherit eutils haskell-cabal

DESCRIPTION="The Haskell-Scriptable Editor"
HOMEPAGE="http://haskell.org/haskellwiki/Yi"
SRC_URI="http://hackage.haskell.org/packages/archive/${PN}/${PV}/${P}.tar.gz"

# The home page is missing information, so I note some here:
# There are now multiple repos after the code.haskell.org outage, not sure which is the master.
# The Mercurial repo on Google code has the bug database:
# http://code.google.com/p/yi-editor/
# The github repo has recent activity:
# https://github.com/yi-editor/yi

LICENSE="GPL-2"
SLOT="0"
KEYWORDS="~amd64 ~x86"
IUSE="gnome gtk vty"

RDEPEND=">=dev-lang/ghc-6.12.1
		=dev-haskell/binary-0.5*
		<dev-haskell/cabal-1.13
		<dev-haskell/cautious-file-0.2
		<dev-haskell/data-accessor-0.3
		=dev-haskell/data-accessor-monads-fd-0.2*
		<dev-haskell/data-accessor-template-0.2.2
		>dev-haskell/derive-2.4
		=dev-haskell/diff-0.1*
		>=dev-haskell/dlist-0.4.1
		>=dev-haskell/dyre-0.7
		<dev-haskell/fingertree-0.1
		=dev-haskell/ghc-paths-0.1*
		>dev-haskell/hint-0.3.1
		>=dev-haskell/monads-fd-0.1.0.1
		<dev-haskell/pointedlist-0.4
		>=dev-haskell/puremd5-0.2.3
		>=dev-haskell/quickcheck-2.1.0.2
		=dev-haskell/regex-base-0.93*
		=dev-haskell/regex-tdfa-1.1*
		=dev-haskell/rosezipper-0.1*
		=dev-haskell/split-0.1*
		<dev-haskell/time-1.3
		dev-haskell/uniplate
		<dev-haskell/unix-compat-0.4
		>=dev-haskell/utf8-string-0.3.1
		vty? ( =dev-haskell/vty-4* )
		gtk? ( =dev-haskell/glib-0.12*
			   =dev-haskell/gtk-0.12*
			   gnome? ( =dev-haskell/gconf-0.12* ) )"
DEPEND="${RDEPEND}
		dev-haskell/alex
		>=dev-haskell/cabal-1.10"

src_prepare() {
	# The haddock documentation fails to build with cabal 1.10 style cabal files.
	# http://hackage.haskell.org/trac/hackage/ticket/656
	# Workaround is to revert the cabal file back to using the old style.
	cd "${S}"
	epatch "${FILESDIR}/${P}-old-style-cabal.patch"
	# Ugh! Upstream forgot it.
	cp "${FILESDIR}/yi-0.6.3.0-src-library-Yi-UI-Pango-Gnome.hs" \
		"${S}/src/library/Yi/UI/Pango/Gnome.hs"
	# Upstream forgot the images required for the gtk pango frontend.
	# The yi github repo yi/src/library/Yi/UI/Pango.hs calls it yi+lambda-fat-32.png
	# The yi-0.6.3.0/src/library/Yi/UI/Pango.hs calls it yi+lambda-fat.32.png
	cp "${FILESDIR}/yi+lambda-fat-16.png" "${S}/art/yi+lambda-fat-16.png"
	cp "${FILESDIR}/yi+lambda-fat-32.png" "${S}/art/yi+lambda-fat-32.png"
	cp "${FILESDIR}/yi+lambda-fat-64.png" "${S}/art/yi+lambda-fat-64.png"
	cp "${FILESDIR}/yi+lambda-fat-128.png" "${S}/art/yi+lambda-fat-128.png"
	sed -e 's@ico <- loadIcon "yi+lambda-fat.32.png"@ico <- loadIcon "yi+lambda-fat-32.png"@' \
		-i "${S}/src/library/Yi/UI/Pango.hs"
	sed -e 's@derive >=2.3 && <2.5@derive >=2.3 \&\& <2.6@' \
		-e 's@Cabal >= 1.10 && < 1.11@Cabal >= 1.10 \&\& < 1.13@' \
		-e 's@unix-compat >=0.1 && <0.3@unix-compat >=0.1 \&\& <0.4@g' \
		-i "${S}/${PN}.cabal" || die "Could not loosen dependencies"
}

src_configure() {
	cabal_src_configure \
		--flags=-testing \
		$(cabal_flag gtk pango) \
		$(cabal_flag gnome) \
		$(cabal_flag vty)

	use gtk || use vty || ewarn "${PN} requires either USE=gtk or USE=vty to build a user interface."
}
