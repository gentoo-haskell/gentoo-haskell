# Copyright 1999-2012 Gentoo Foundation
# Distributed under the terms of the GNU General Public License v2
# $Header: $

EAPI=5

# ebuild generated by hackport 0.3.1.9999

CABAL_FEATURES="bin lib profile haddock hoogle hscolour test-suite"
CABAL_FEATURES+=" nocabaldep" # needs ghc's version as uses ghc's 'bin-package-db' linked against Cabal
inherit base haskell-cabal

DESCRIPTION="Metadata collection for leksah"
HOMEPAGE="http://leksah.org"
SRC_URI="mirror://hackage/packages/archive/${PN}/${PV}/${P}.tar.gz"

LICENSE="GPL-2"
SLOT="0/${PV}"
KEYWORDS="~amd64 ~x86"
IUSE=""

RDEPEND=">=dev-haskell/attoparsec-0.10.0.3:=[profile?]
		=dev-haskell/attoparsec-enumerator-0.3*:=[profile?]
		>=dev-haskell/binary-0.5.0.0:=[profile?]
		<dev-haskell/binary-0.8:=[profile?]
		=dev-haskell/binary-shared-0.8*:=[profile?]
		>=dev-haskell/cabal-1.6.0.1:=[profile?]
		>=dev-haskell/enumerator-0.4.14:=[profile?]
		<dev-haskell/enumerator-0.5:=[profile?]
		>=dev-haskell/haddock-2.7.2:=[profile?]
		<dev-haskell/haddock-2.14:=[profile?]
		>=dev-haskell/hslogger-1.0.7:=[profile?]
		<dev-haskell/hslogger-1.3:=[profile?]
		>=dev-haskell/ltk-0.12.1.0:=[profile?]
		<dev-haskell/ltk-0.13:=[profile?]
		>=dev-haskell/network-2.2:=[profile?]
		<dev-haskell/network-3.0:=[profile?]
		>=dev-haskell/parsec-2.1.0.1:=[profile?]
		<dev-haskell/parsec-3.2:=[profile?]
		>=dev-haskell/process-leksah-1.0.1.3:=[profile?]
		<dev-haskell/process-leksah-1.1:=[profile?]
		>=dev-haskell/strict-0.3.2:=[profile?]
		<dev-haskell/strict-0.4:=[profile?]
		>=dev-haskell/transformers-0.2.2.0:=[profile?]
		<dev-haskell/transformers-0.4:=[profile?]
		>=dev-lang/ghc-6.12.1:="
DEPEND="${RDEPEND}
		test? ( =dev-haskell/hunit-1.2*
		)
		>=dev-haskell/cabal-1.10.2"

PATCHES=("${FILESDIR}/${PN}-0.12.1.2-ghc-7.6.patch")

src_prepare() {
	base_src_prepare
	if has_version "<dev-lang/ghc-7.0.1" && has_version ">=dev-haskell/cabal-1.10.0.0"; then
		# with ghc 6.12 does not work with cabal-1.10, so use ghc-6.12 shipped one
		sed -e 's@build-depends: Cabal >=1.6.0.1 && <1.18@build-depends: Cabal >=1.6.0.1 \&\& <1.9@g' \
			-i "${S}/${PN}.cabal"
	fi
	if [ $(ghc-pkg list Cabal | grep -c Cabal) -gt 1 ]; then
		ewarn "The command"
		ewarn ""
		ewarn "ghc-pkg list Cabal"
		ewarn ""
		ewarn "says there is more than 1 Cabal version installed."
		ewarn "This may cause a build failure, something like:"
		ewarn ""
		ewarn "src/IDE/Core/CTypes.hs:101:23-41:"
		ewarn "Couldn\'t match type \`IPI.InstalledPackageInfo_ m0\'"
		ewarn "with \`Cabal-1.16.0:Distribution.InstalledPackageInfo.InstalledPackageInfo_"
		ewarn "                          Module.ModuleName\'"
		ewarn "    Expected type: PackageConfig -> PackageIdentifier"
		ewarn "      Actual type: IPI.InstalledPackageInfo_ m0"
		ewarn "                   -> Distribution.Package.PackageId"
		ewarn "    In the expression: IPI.sourcePackageId"
		ewarn "    In an equation for \`getThisPackage\':"
		ewarn "getThisPackage = IPI.sourcePackageId"
		ewarn ""
		ewarn "To avoid this type of compilation error, configure"
		ewarn "${ROOT}etc/portage/package.keywords"
		ewarn "to select the same Cabal version as used dev-lang/ghc.  For example, for"
		ewarn "dev-lang/ghc-7.6.1 the following line in"
		ewarn "${ROOT}etc/portage/package.keywords"
		ewarn "ensures that dev-haskell/cabal-1.16.0 is selected"
		ewarn ""
		ewarn "<dev-haskell/cabal-1.16.0.1"
	fi
	cabal_chdeps \
		'attoparsec >=0.10.0.3 && <0.11' 'attoparsec >=0.10.0.3' \
		'binary >=0.5.0.0 && <0.7' 'binary >=0.5.0.0 && <0.8'
}

src_configure() {
	threaded_flag=""
	if $(ghc-getghc) --info | grep "Support SMP" | grep -q "YES"; then
		threaded_flag="--flags=threaded"
		einfo "$P will be built with threads support"
	else
		threaded_flag="--flags=-threaded"
		einfo "$P will be built without threads support"
	fi
	cabal_src_configure $threaded_flag \
		$(use_enable test tests) \
		--constraint="Cabal == $(cabal-version)"
}
