# Copyright 1999-2020 Gentoo Authors
# Distributed under the terms of the GNU General Public License v2

EAPI=6

# ebuild generated by hackport 0.4.6

CABAL_FEATURES="bin"
inherit elisp-common haskell-cabal

DESCRIPTION="Structured editing Emacs mode for Haskell"
HOMEPAGE="https://github.com/chrisdone/structured-haskell-mode"
SRC_URI="https://hackage.haskell.org/package/${P}/${P}.tar.gz"

LICENSE="BSD"
SLOT="0"
KEYWORDS="~amd64 ~x86"
IUSE="emacs"

RDEPEND=""
DEPEND="${RDEPEND}
	dev-haskell/applicative-quoters
	>=dev-haskell/cabal-1.8
	>=dev-haskell/descriptive-0.7 <dev-haskell/descriptive-0.10
	>=dev-haskell/haskell-src-exts-1.16 <dev-haskell/haskell-src-exts-1.18
	dev-haskell/text
	>=dev-lang/ghc-7.4.1
	emacs? (
		app-emacs/hindent
		>=app-editors/emacs-23.1:* )
"
SITEFILE="50${PN}-gentoo.el"

PATCHES=( "${FILESDIR}/${PN}-1.0.4.patch" )

src_prepare() {
	default
	cabal_chdeps \
		'haskell-src-exts == 1.16.*' 'haskell-src-exts >= 1.16 && < 1.18'
}

src_compile() {
	haskell-cabal_src_compile
	if use emacs; then
		pushd elisp
		elisp-compile *.el || die
		popd
	fi
}

src_install() {
	haskell-cabal_src_install
	if use emacs; then
		elisp-install "${PN}" elisp/*.{el,elc}
		elisp-site-file-install "${FILESDIR}/${SITEFILE}"
	fi
}

pkg_postinst() {
	haskell-cabal_pkg_postinst
	if use emacs; then
		elisp-site-regen
	fi
}

pkg_postrm() {
	haskell-cabal_pkg_postrm
	if use emacs; then
		elisp-site-regen
	fi
}
