# Copyright 1999-2012 Gentoo Foundation
# Distributed under the terms of the GNU General Public License v2
# $Header: $

EAPI=5

# ebuild generated by hackport 0.3.1.9999

GTK_MAJ_VER="2"

CABAL_FEATURES="lib profile haddock hoogle hscolour"
CABAL_FEATURES+=" nocabaldep" # needs ghc's version as used by leksah
inherit base haskell-cabal

DESCRIPTION="Leksah tool kit"
HOMEPAGE="http://www.leksah.org"
SRC_URI="mirror://hackage/packages/archive/${PN}/${PV}/${P}.tar.gz"

LICENSE="GPL-2"
SLOT="0/${PV}"
KEYWORDS="~amd64 ~x86"
IUSE=""

RDEPEND=">=dev-haskell/cabal-1.6.0:=[profile?]
		<dev-haskell/cabal-1.18:=[profile?]
		>=dev-haskell/glib-0.10.0:${GTK_MAJ_VER}=[profile?]
		<dev-haskell/glib-0.13:${GTK_MAJ_VER}=[profile?]
		>=dev-haskell/gtk-0.10.0:${GTK_MAJ_VER}=[profile?]
		<dev-haskell/gtk-0.13:${GTK_MAJ_VER}=[profile?]
		>=dev-haskell/mtl-1.1.0.2:=[profile?]
		<dev-haskell/mtl-2.2:=[profile?]
		>=dev-haskell/parsec-2.1.0.1:=[profile?]
		<dev-haskell/parsec-3.2:=[profile?]
		>=dev-haskell/transformers-0.2.2.0:=[profile?]
		<dev-haskell/transformers-0.4:=[profile?]
		>=dev-lang/ghc-6.10.4:="
DEPEND="${RDEPEND}
		>=dev-haskell/cabal-1.8"

PATCHES=("${FILESDIR}/${PN}-0.12.1.0-ghc-7.6.patch")

src_prepare() {
	base_src_prepare
	if has_version "<dev-lang/ghc-7.0.1" && has_version ">=dev-haskell/cabal-1.10.0.0"; then
		# with ghc 6.12 leksah-server does not work with cabal-1.10, so use ghc-6.12 shipped one
		# since leksah-server uses cabal, haddock, and ltk, ltk must use ghc 6.12 cabal for ghc < 7.
		sed -e 's@build-depends: Cabal >=1.6.0 && <1.18@build-depends: Cabal >=1.6.0 \&\& <1.9@g' \
			-i "${S}/${PN}.cabal"
	fi
}

src_configure() {
	cabal_src_configure \
		--constraint="Cabal == $(cabal-version)"
}
