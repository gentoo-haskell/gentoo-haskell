# Copyright 1999-2012 Gentoo Foundation
# Distributed under the terms of the GNU General Public License v2
# $Header: $

EAPI=5

# ebuild generated by hackport 0.3.9999

CABAL_FEATURES="bin"
inherit haskell-cabal bash-completion-r1 eutils git-2

MY_PN="cabal-install"
MY_PV=$(get_version_component_range '1-3')
MY_P="${MY_PN}-${PV}"

DESCRIPTION="The command-line interface for Cabal and Hackage."
HOMEPAGE="http://www.haskell.org/cabal/"
EGIT_REPO_URI="git://github.com/haskell/cabal.git"

LICENSE="BSD"
SLOT="0"
KEYWORDS="~alpha ~amd64 ~ia64 ~ppc64 ~sparc ~x86"
IUSE="+noprefs"

RDEPEND=""
DEPEND="${RDEPEND}
		>=dev-haskell/cabal-1.17.0
		>=dev-haskell/http-4000.0.2
		<dev-haskell/http-4001
		>=dev-haskell/mtl-2.0
		<dev-haskell/mtl-3
		>=dev-haskell/network-1
		<dev-haskell/network-3
		>=dev-haskell/random-1
		<dev-haskell/random-1.1
		>=dev-haskell/zlib-0.4
		<dev-haskell/zlib-0.6
		>=dev-haskell/stm-2.0
		<dev-haskell/stm-3.0
		>=dev-lang/ghc-6.10.4"

S="${WORKDIR}/${MY_PN}"

src_prepare() {
	# because we need cabal-install subdir ( I'm not sure how to make it in better way :3 )
	cp -rf "${S}/cabal-install" "${WORKDIR}" || die
	if use noprefs; then
		epatch "${FILESDIR}/${PN}"-9999-nopref.patch
	fi
	CABAL_FILE=${S}/${MY_PN}.cabal \
	cabal_chdeps 'process    >= 1   && < 1.2' 'process    >= 1   && < 1.3'
}

src_install() {
	haskell-cabal_src_install

	newbashcomp "${S}/bash-completion/cabal" ${PN}
}
