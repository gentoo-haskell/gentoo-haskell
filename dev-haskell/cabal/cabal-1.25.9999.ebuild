# Copyright 1999-2016 Gentoo Foundation
# Distributed under the terms of the GNU General Public License v2

EAPI=6

# ebuild generated by hackport 0.5.9999
#hackport: flags: -bundled-binary-generic

CABAL_FEATURES="lib profile test-suite"
CABAL_FEATURES+=" nocabaldep" # in case installed Cabal is broken
inherit haskell-cabal

if [[ ${PV} == *9999* ]]; then
	LIVE_EBUILD=yes
	inherit git-r3
fi

MY_PN="Cabal"
MY_P="${MY_PN}-${PV}"

DESCRIPTION="A framework for packaging Haskell software"
HOMEPAGE="http://www.haskell.org/cabal/"
EGIT_REPO_URI="https://github.com/haskell/cabal.git"

LICENSE="BSD"
SLOT="0/${PV}"
IUSE=""

if [[ -n ${LIVE_EBUILD} ]]; then
	# Cabal's subdir
	S="${S}"/${MY_PN}
else
	SRC_URI="mirror://hackage/packages/archive/${PN}/${PV}/${P}.tar.gz"
	KEYWORDS="~alpha ~amd64 ~ia64 ~ppc ~ppc64 ~sparc ~x86 ~amd64-linux ~x86-linux ~ppc-macos ~x86-macos ~x86-solaris"
	S="${WORKDIR}/${MY_PN}"
fi

RESTRICT=test # circular deps: cabal -> quickcheck -> cabal

RDEPEND=">=dev-lang/ghc-7.8.1:="
DEPEND="${RDEPEND}
	test? ( dev-haskell/extensible-exceptions
		dev-haskell/hunit
		>=dev-haskell/quickcheck-2.1.0.1
		dev-haskell/regex-posix
		dev-haskell/test-framework
		dev-haskell/test-framework-hunit
		>=dev-haskell/test-framework-quickcheck2-0.2.12 )
"

src_prepare() {
	local cabal_upstream_version=$(sed -n 's/^version: //ip' ${MY_PN}.cabal)

	if [[ -n ${LIVE_EBUILD} ]]; then
		# one of renaming reasons is to avoid clashing with bundled ghc-cabal
		CABAL_FILE=${MY_PN}.cabal cabal_chdeps "version: ${cabal_upstream_version}" "version: ${PV}"
	fi
	default

	# bootstrap uses full new source of Cabal. Not all extensions are present in .hs file.
	GHC_BOOTSTRAP_FLAGS="${GHC_BOOTSTRAP_FLAGS} -XRank2Types"
}

src_configure() {
	haskell-cabal_src_configure \
		--flag=-bundled-binary-generic
}
