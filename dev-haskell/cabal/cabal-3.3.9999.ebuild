# Copyright 1999-2019 Gentoo Authors
# Distributed under the terms of the GNU General Public License v2

EAPI=7

# ebuild generated by hackport 0.6.1.9999
#hackport: flags: -bundled-binary-generic

CABAL_FEATURES="lib profile" # drop test-suite: circular depend
CABAL_FEATURES+=" nocabaldep" # in case installed cabal is broken
inherit haskell-cabal git-r3

LIVE_EBUILD=yes

MY_PN="Cabal"
MY_P="${MY_PN}-${PV}"

DESCRIPTION="A framework for packaging Haskell software"
HOMEPAGE="https://www.haskell.org/cabal/"
EGIT_REPO_URI="https://github.com/haskell/cabal.git"

LICENSE="BSD"
SLOT="0/${PV}"
IUSE=""

if [[ -n ${LIVE_EBUILD} ]]; then
	# Cabal's subdir
	S="${S}"/${MY_PN}
else
	SRC_URI="https://hackage.haskell.org/package/${P}/${P}.tar.gz"
	KEYWORDS="~amd64 ~x86 ~amd64-linux ~x86-linux"
	S="${WORKDIR}/${MY_PN}"
fi

RESTRICT=test # circular deps: cabal -> quickcheck -> cabal

RDEPEND=">=dev-haskell/fail-4.9:=[profile?] <dev-haskell/fail-4.10:=[profile?]
	>=dev-haskell/mtl-2.1:=[profile?] <dev-haskell/mtl-2.3:=[profile?]
	>=dev-haskell/parsec-3.1.13.0:=[profile?] <dev-haskell/parsec-3.2:=[profile?]
	>=dev-haskell/semigroups-0.18.3:=[profile?] <dev-haskell/semigroups-0.20:=[profile?]
	>=dev-haskell/text-1.2.3.0:=[profile?] <dev-haskell/text-1.3:=[profile?]
	>=dev-lang/ghc-7.8.2:=
"
DEPEND="${RDEPEND}"

src_prepare() {
	default

	local cabal_upstream_version=$(sed -n 's/^version: //ip' ${MY_PN}.cabal)

	if [[ -n ${LIVE_EBUILD} ]]; then
		# one of renaming reasons is to avoid clashing with bundled ghc-cabal
		CABAL_FILE=${MY_PN}.cabal cabal_chdeps "version: ${cabal_upstream_version}" "version: ${PV}"
	fi
}

src_configure() {
	haskell-cabal_src_configure \
		--flag=-bundled-binary-generic
}
