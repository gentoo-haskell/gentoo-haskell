# Copyright 1999-2021 Gentoo Authors
# Distributed under the terms of the GNU General Public License v2

EAPI=8

# ebuild generated by hackport 0.7.9999

CABAL_FEATURES="lib profile haddock hoogle hscolour test-suite"
inherit haskell-cabal

DESCRIPTION="Generates a references DB from .hie files"
HOMEPAGE="https://hackage.haskell.org/package/hiedb"
SRC_URI="https://hackage.haskell.org/package/${P}/${P}.tar.gz"

LICENSE="BSD"
SLOT="0/${PV}"
KEYWORDS="~amd64 ~x86"
IUSE="executable"

PATCHES=(
	"${FILESDIR}/${PN}-0.3.0.1-add-executable-flag.patch"
)

RDEPEND="dev-haskell/algebraic-graphs:=[profile?]
	dev-haskell/ansi-terminal:=[profile?]
	dev-haskell/extra:=[profile?]
	dev-haskell/hie-compat:=[profile?]
	dev-haskell/lucid:=[profile?]
	dev-haskell/mtl:=[profile?]
	dev-haskell/optparse-applicative:=[profile?]
	dev-haskell/sqlite-simple:=[profile?]
	dev-haskell/terminal-size:=[profile?]
	dev-haskell/text:=[profile?]
	>=dev-lang/ghc-8.6.3:=
	executable? ( dev-haskell/ghc-paths:=[profile?] )
"
DEPEND="${RDEPEND}
	>=dev-haskell/cabal-2.4.0.1
	test? ( dev-haskell/ghc-paths
	    dev-haskell/hspec
		dev-haskell/temporary )
"

src_configure() {
	if use executable || use test; then
		local myExeFlag=executable
	else
		local myExeFlag=-executable
	fi
	haskell-cabal_src_configure \
		--flag="${myExeFlag}"
}

src_install() {
	if use test && ! use executable; then
		# Only install lib (skip exe)
		haskell-cabal_src_install "lib:${PN}"
	else
		# Install everything
		haskell-cabal_src_install
	fi
}

src_test() {
	# Needed to find `hiedb` executable in build path
	export PATH="${S}/dist/build/${PN}${PATH+:}${PATH}"
	haskell-cabal_src_test
}
