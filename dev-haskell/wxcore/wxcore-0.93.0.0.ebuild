# Copyright 1999-2025 Gentoo Authors
# Distributed under the terms of the GNU General Public License v2

EAPI=8

# ebuild generated by hackport 0.5.3.9999

WX_GTK_VER="3.2-gtk3"

CABAL_FEATURES="lib profile haddock hoogle hscolour"
inherit haskell-cabal wxwidgets

DESCRIPTION="wxHaskell core"
HOMEPAGE="https://wiki.haskell.org/WxHaskell"
SRC_URI="https://codeberg.org/wxHaskell/wxHaskell/archive/wxc-1.0.0.2.tar.gz"
S="${WORKDIR}/wxhaskell/wxcore"

LICENSE="wxWinLL-3.1"
SLOT="0/${PV}"
KEYWORDS="~amd64"
IUSE="gstreamer"

PATCHES=(
	"${FILESDIR}"/${P}-fix-configure.patch
	)

RDEPEND="dev-haskell/parsec:=[profile?]
	>=dev-lang/ghc-9.0.2:=
	>=dev-haskell/wxc-1.0.0.2
"
DEPEND="${RDEPEND}
	>=dev-haskell/cabal-3.4.1.0
	>=dev-haskell/wxdirect-0.93
	virtual/pkgconfig
"
src_prepare() {
	default

	sed -e "s@wxdirect@wxdirect-${WX_GTK_VER}@g" \
		-i "${S}/Setup.hs" \
		|| die "Could not change Setup.hs for wxdirect slot ${WX_GTK_VER}"
	sed -e "s@wxdirect@wxdirect-${WX_GTK_VER}@g" \
		-i "${S}/configure" \
		|| die "Could not change configure for wxdirect slot ${WX_GTK_VER}"
}

src_configure() {
	haskell-cabal_src_configure \
		--flag=splitbase
}
