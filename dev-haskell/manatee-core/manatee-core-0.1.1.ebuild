# Copyright 1999-2013 Gentoo Foundation
# Distributed under the terms of the GNU General Public License v2
# $Header:  $

# ebuild generated by hackport 0.2.13

EAPI="3"

CABAL_FEATURES="lib profile haddock hscolour"
inherit haskell-cabal

DESCRIPTION="The core of Manatee"
HOMEPAGE="http://hackage.haskell.org/package/manatee-core"
SRC_URI="mirror://hackage/packages/archive/${PN}/${PV}/${P}.tar.gz"

LICENSE="GPL-3"
SLOT="0"
KEYWORDS="~amd64 ~x86"
IUSE=""

RDEPEND="dev-haskell/binary
		>=dev-haskell/cabal-1.8.0.2
		>=dev-haskell/cairo-0.12.0
		dev-haskell/dataenc
		=dev-haskell/dbus-client-0.4*
		dev-haskell/dbus-core
		dev-haskell/derive
		>=dev-haskell/gconf-0.12.0
		dev-haskell/ghc-paths
		>=dev-haskell/gio-0.12.0
		>=dev-haskell/glib-0.12.0
		>=dev-haskell/gtk-0.12.0
		>=dev-haskell/gtk-serialized-event-0.12.0
		>=dev-haskell/gtksourceview-0.12.0:2
		dev-haskell/haskell-src-exts
		>=dev-haskell/mtl-1.1.0.2
		>=dev-haskell/network-2.2.1.5
		>=dev-haskell/regex-tdfa-1.1.2
		dev-haskell/split
		>=dev-haskell/stm-2.1.2.0
		>=dev-haskell/text-0.7.1.0
		>=dev-haskell/utf8-string-0.3.4
		>=dev-lang/ghc-6.12.1"
DEPEND="${RDEPEND}
		>=dev-haskell/cabal-1.6"

src_prepare() {
	sed -e 's@dbus-client >= 0.3 && < 0.4@dbus-client >= 0.3 \&\& < 0.5@' \
		-i "${S}/${PN}.cabal" || die "Could not loosen dbus-client dependency"
	epatch "${FILESDIR}/${P}-dbus-client-0.4.patch"
	epatch "${FILESDIR}/${PN}-STM.patch"
	epatch "${FILESDIR}/${PN}-import-Avail.patch"
	sed -e '/programFindLocation/s/$/ [ProgramSearchPathDefault]/' \
		-i Manatee/Toolkit/General/Process.hs || \
		die "Could not build with >=cabal-1.18"
	sed -e 's/defaultCallbacks//' -i Manatee/Core/Dynload.hs || die
	epatch "${FILESDIR}/${PN}-ghc-7.6.patch"
}
