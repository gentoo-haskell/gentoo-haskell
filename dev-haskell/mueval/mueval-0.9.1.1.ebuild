# Copyright 1999-2014 Gentoo Foundation
# Distributed under the terms of the GNU General Public License v2
# $Header: $

EAPI=5

# ebuild generated by hackport 0.3.5.9999

CABAL_FEATURES="bin lib profile haddock hoogle hscolour"
inherit haskell-cabal pax-utils

DESCRIPTION="Safely evaluate pure Haskell expressions"
HOMEPAGE="https://github.com/gwern/mueval"
SRC_URI="mirror://hackage/packages/archive/${PN}/${PV}/${P}.tar.gz"

LICENSE="BSD"
SLOT="0/${PV}"
KEYWORDS="~amd64 ~x86"
IUSE=""

RDEPEND="dev-haskell/cabal:=[profile?]
	dev-haskell/extensible-exceptions:=[profile?]
	>=dev-haskell/hint-0.3.1:=[profile?]
	>dev-haskell/mtl-2:=[profile?]
	>=dev-haskell/show-0.3:=[profile?]
	dev-haskell/simple-reflect:=[profile?]
	dev-haskell/utf8-string:=[profile?]
	>=dev-lang/ghc-6.10.4:=
"
DEPEND="${RDEPEND}
	>=dev-haskell/cabal-1.6.0.3
"

# default mueval timeout. in 0.7-second units
# Handy to change as lambdabot does not
# allow this bit to confgure
: ${MUEVAL_TIMEOUT:=5}

CABAL_EXTRA_BUILD_FLAGS+=" --ghc-options=-rtsopts"

src_prepare() {
	einfo "Using default mueval timeout: ${MUEVAL_TIMEOUT} * 0.7s"
	sed -e "s@timeLimit = 5@timeLimit = ${MUEVAL_TIMEOUT}@" \
		-i "${S}"/Mueval/ArgsParse.hs || die
}

src_install() {
	cabal_src_install
	# uses GHC-api which needs mmap('rwx') (bug #299709)
	pax-mark -m "${ED}/usr/bin/mueval-core"
}
