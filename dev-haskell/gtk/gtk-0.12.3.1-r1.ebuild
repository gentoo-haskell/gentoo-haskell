# Copyright 1999-2013 Gentoo Foundation
# Distributed under the terms of the GNU General Public License v2
# $Header: $

EAPI=4

# ebuild generated by hackport 0.2.18.9999

GTK_MAJ_VER="2"

#nocabaldep is for the fancy cabal-detection feature at build-time
CABAL_FEATURES="lib profile haddock hscolour hoogle nocabaldep"
inherit haskell-cabal

DESCRIPTION="Binding to the Gtk+ graphical user interface library."
HOMEPAGE="http://projects.haskell.org/gtk2hs/"
SRC_URI="mirror://hackage/packages/archive/${PN}/${PV}/${P}.tar.gz"

LICENSE="LGPL-2.1"
SLOT="${GTK_MAJ_VER}"
KEYWORDS="~amd64 ~x86"
IUSE="+gio"

RDEPEND=">=dev-haskell/cairo-0.12.0:${GTK_MAJ_VER}[profile?]
		<dev-haskell/cairo-0.13:${GTK_MAJ_VER}[profile?]
		>=dev-haskell/glib-0.12.0:${GTK_MAJ_VER}[profile?]
		<dev-haskell/glib-0.13:${GTK_MAJ_VER}[profile?]
		dev-haskell/mtl[profile?]
		>=dev-haskell/pango-0.12.0:${GTK_MAJ_VER}[profile?]
		<dev-haskell/pango-0.13:${GTK_MAJ_VER}[profile?]
		>=dev-lang/ghc-6.10.1
		dev-libs/glib:2
		x11-libs/gtk+:${GTK_MAJ_VER}
		gio? ( =dev-haskell/gio-0.12*:${GTK_MAJ_VER}[profile?] )"
DEPEND="${RDEPEND}
		>=dev-haskell/cabal-1.8
		dev-haskell/gtk2hs-buildtools:${GTK_MAJ_VER}"

src_prepare() {
	# c2hs ignores #if __GLASGOW_HASKELL__ >= 706
	# I do not know which earlier ghc versions the patch submitted upstream works with
	if has_version ">=dev-lang/ghc-7.6.1"; then
		epatch "${FILESDIR}/${PN}-0.12.3.1-ghc-7.6.patch"
	fi
	sed -e "s@gtk2hsTypeGen@gtk2hsTypeGen${GTK_MAJ_VER}@" \
		-e "s@gtk2hsHookGenerator@gtk2hsHookGenerator${GTK_MAJ_VER}@" \
		-e "s@gtk2hsC2hs@gtk2hsC2hs${GTK_MAJ_VER}@" \
		-i "${S}/Gtk2HsSetup.hs" \
		|| die "Could not change Gtk2HsSetup.hs for GTK+ slot ${GTK_MAJ_VER}"
	sed -e "s@gtk2hsC2hs@gtk2hsC2hs${GTK_MAJ_VER}@" \
		-e "s@gtk2hsTypeGen@gtk2hsTypeGen${GTK_MAJ_VER}@" \
		-e "s@gtk2hsHookGenerator@gtk2hsHookGenerator${GTK_MAJ_VER}@" \
		-i "${S}/${PN}.cabal" \
		|| die "Could not change ${PN}.cabal for GTK+ slot ${GTK_MAJ_VER}"
}

src_configure() {
	# Upstream has this enabled, so we might as well force it enabled to be sure.
	cabal_src_configure \
		--flags=deprecated \
		$(cabal_flag gio have-gio)
}
